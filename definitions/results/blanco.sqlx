/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    type: "incremental",
    schema: constants.RESULTS_DATASET,
    description: "All private loan related tracking",
    tags: ["all"],
    bigquery: {
      partitionBy: "date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

with applications as (
  select
    *
  from
    ${ref('applications')}
  where
    date >= event_date_checkpoint
    and product = 'Privatlån'
)

  select
    date,
    session_id,
    type,
    event_name,
    status,
    pages_in_session,
    step,
    previous_submit_location,
    source_medium,
    event_params.co_applicant as co_applicant,
    case
      when regexp_contains(event_name, 'start') then 'Start'
      when regexp_contains(step, '1') then 'Steg 1 - Frågor om lånet'
      when regexp_contains(step, '2') then 'Steg 2 - Uppgifter om dig'
      when regexp_contains(step, 'Uppgifter om din medsökande') then 'Steg 2.5 - Uppgifter om din medsökande'
      when regexp_contains(step, 'Ekonomi & levnadskostnader') then 'Steg 3 - Ekonomi & levnadskostnader'
      when regexp_contains(step, 'Syfte med lånet') then 'Steg 4 - Syfte med lånet'
      when regexp_contains(step, 'Godkänn villkor & ansök') then 'Steg 5 - Godkänn villkor & ansök'
      when regexp_contains(step, 'Submit') then 'Submit'
      when event_name = 'blanco_application_sign_click' or event_name = 'blanco_application_signed' then 'Sign'
      when event_name = 'blanco_autogiro_sign_click' then 'Autogiro sign'
  else '99' end as sort,
  from
    applications
  where date >= '2024-01-10'
  group by
    date,
    session_id,
    type,
    event_name,
    status,
    pages_in_session,
    step,
    previous_submit_location,
    source_medium,
    co_applicant