/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    type: "incremental",
    schema: constants.RESULTS_DATASET,
    description: "All events connected to applications to use in analysis or in looker reports with focus on performance",
    tags: ["all"],
    bigquery: {
      partitionBy: "date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

select
  date,
  session_id,
  channel_group,
  source_medium,
  product,
  case 
    when product != 'PrivatlÃ¥n' then type
  else null end as type,
  previous_submit_location,
  status,
  case when event_name = 'page_view' and not regexp_contains(page_location, 'ansokan-|ansok-|slutford-signering') then session_id else null end as visits,
  case when event_name = 'mortgage_indicator_nav' or event_name = 'calculator_nav' then session_id else null end as used_calculator,
  case when regexp_contains(event_name, 'application_start') then session_id else null end as starts,
  case when regexp_contains(event_name, 'application_subm') then session_id else null end as submits,
  case when regexp_contains(event_name, 'application_subm') and status = 'AdditionalInformationNeeded' then session_id else null end as submits_approved,
  case when regexp_contains(event_name, 'application_subm') and status = 'Review' then session_id else null end as submits_review,
  case when regexp_contains(event_name, 'application_subm') and status = 'Rejected' then session_id else null end as submits_rejected,
  sum(event_value) as event_value
from
  ${ref('applications')}
where 
  date >= event_date_checkpoint
--session_id = 'zzVL8WwO1B1veicqdA36FuKpvB+428oSixjNmAKipDM=.17169220881716922087'
group by 
  date,
  session_id,
  channel_group,
  source_medium,
  product,
  type,
  previous_submit_location,
  status,
  visits,
  used_calculator,
  starts,
  submits,
  submits_approved,
  submits_review,
  submits_rejected