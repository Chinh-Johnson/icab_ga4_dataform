config {
    type: "incremental",
    schema: constants.RESULTS_DATASET,
    description: "All mortgage related tracking",
    tags: ["all"],
    bigquery: {
      partitionBy: "date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

with applications as (
  select
    *
  from
    ${ref('applications')}
  where
    date >= event_date_checkpoint
    and product = 'Bol√•n'
)

  select
    date,
    session_id,
    type,
    event_name,
    status,
    pages_in_session,
    step,
    previous_submit_location,
    source_medium,
    sum(event_value) as event_value,
  from
    applications
  group by
    date,
    session_id,
    type,
    event_name,
    status,
    pages_in_session,
    step,
    previous_submit_location,
    source_medium