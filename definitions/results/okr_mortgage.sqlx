/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    type: "table",
    schema: constants.RESULTS_DATASET,
    description: "OKR of conversion rate for mortgage, used in looker studio and tableau. Denominator will count the number of unique sessions that has vistied a specific /bolan/ page with specific conditions. Achieved_value will count the number of unique sessions where a submit event has happened within the same session",
    tags: ["all"],
}

with applications as (
  select
    date,
    session_id,
    LOGICAL_OR(REGEXP_CONTAINS(page_location, '/bolan/') or REGEXP_CONTAINS(page_location, '/lana/hemnetkalkyl/')) as visited_mortgage,
    LOGICAL_OR(REGEXP_CONTAINS(event_name, 'transfer_mortgage_application_submit|new_mortgage_application_submit|commitment_mortgage_application_submit|raise_mortgage_application_submit|loan_promise_application_submit')) as conversions,
    page_location,
    landing_page,
    source_medium
  from
    ${ref('applications')}
  where
    product = 'Bol√•n'
    and regexp_contains(event_name, 'page_view|submit')
  group by
    date,
    session_id,
    page_location,
    landing_page,
    source_medium
)

  select
    d.date as Date,
    cast(format_date('%Y%m%d', d.date) as int64) as DateID, 
    'Conversion Rate Mortgage Submitted Applications (interest to submit)' as KPI_Name_Eng,
    count(distinct case when visited_mortgage and not (regexp_contains(landing_page, '/bolan/|hemnetkalkyl') and not regexp_contains(source_medium, 'appen|email')) then session_id END) as Denominator,
    count(distinct case when conversions and not (regexp_contains(landing_page, '/bolan/') and not regexp_contains(source_medium, 'appen|email')) then session_id END) as Achieved_Value
  from
    applications a
    right outer join ${ref('dates')} d ON a.date = d.date
  group by 
    date