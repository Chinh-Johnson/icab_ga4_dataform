config {
    type: "incremental",
    schema: constants.OUTPUT_DATASET,
    description: "Adform data containing reach, impressions, clicks and cost",
    tags: ["all"],
    bigquery: {
      partitionBy: "Date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

  select
    date,
    'Adform' as platform,
    campaign_name,
    case
      when regexp_contains(campaign_name, 'traffic') then 'Performance'
      when regexp_contains(campaign_name, 'brand') then 'Brand'
    else null end as purpose,
    case
      when regexp_contains(campaign_name, 'privatlan|Privatl책n') then 'Privatl책n'
      when regexp_contains(campaign_name, 'bolan|Bol책n') then 'Bol책n'
      when regexp_contains(campaign_name, 'kort|Kort') then 'Kort'
      when regexp_contains(campaign_name, 'spara|Spara') then 'Spara'
    else null end as product,
    sum(reach) as reach,
    sum(impressions) as impressions,
    sum(clicks) as clicks,
    sum(cost) as cost
  from ${ref('int_adform_metrics')} a
  where Date >= event_date_checkpoint
  group by
    date,
    platform,
    campaign_name