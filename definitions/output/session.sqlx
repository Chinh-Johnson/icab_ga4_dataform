/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
    type: "table",
    schema: constants.OUTPUT_DATASET,
    description: "Sessions model, outputs one row per user session.",
    tags: ["all"],
    bigquery: {
        partitionBy: "session_start_date",
        clusterBy: ["user_key", "ga_session_key"]
    }
}

with session_first_last_source as (
    select
        session_id,
        user_key,
        ga_session_key,
        ga_session_id,
        user_pseudo_id,
        STRING_AGG(REGEXP_REPLACE(page_location, '\\?.*', '') ORDER BY event_timestamp) as pages_in_session,
        ARRAY_AGG(REGEXP_REPLACE(page_location, '\\?.*', '') IGNORE NULLS ORDER BY event_timestamp LIMIT 1)[SAFE_OFFSET(0)] as landing_page,
        MAX(IF(event_name = "login_success", true, false)) as logged_in, 
        --session first traffic source
        array_agg(
            if(coalesce(nullif(collected_traffic_source.source, '(direct)'), nullif(collected_traffic_source.medium, '(none)'), nullif(collected_traffic_source.campaign, '(direct)')) is not null,
                collected_traffic_source,
                null
            )
            order by
                event_timestamp asc,
                event_sort_order asc
            limit 1
        ) [safe_offset(0)] as first_traffic_source,
        --session last non-direct traffic source within the session
        array_agg(
                if(coalesce(nullif(collected_traffic_source.source, '(direct)'), nullif(collected_traffic_source.medium, '(none)'), nullif(collected_traffic_source.campaign, '(direct)')) is not null,
                    collected_traffic_source,
                    null
            ) ignore nulls
            order by
                event_timestamp desc,
                event_sort_order asc
            limit 1
        ) [safe_offset(0)] as last_non_null_traffic_source
    from ${ref('int_event')}
    where ga_session_key is not null
    and event_name not in ('session_start') --this condition could be commented/removed if looking at data post November 2, 2023 when it started being populated.
    group by
        session_id,
        user_key,
        ga_session_key,
        ga_session_id,
        user_pseudo_id
),

sessions_final as (
    SELECT
        * EXCEPT(first_traffic_source, last_non_null_traffic_source),
        STRUCT(
            if(first_traffic_source.source = 'tagassistant.google.com', null, first_traffic_source.source) as source,
            if(first_traffic_source.source = 'tagassistant.google.com', null, first_traffic_source.medium) as medium,
            if(first_traffic_source.source = 'tagassistant.google.com', null, first_traffic_source.campaign) as campaign
        ) as first_traffic_source,
        STRUCT(
            if(last_non_null_traffic_source.source = 'tagassistant.google.com', null, last_non_null_traffic_source.source) as source,
            if(last_non_null_traffic_source.source = 'tagassistant.google.com', null, last_non_null_traffic_source.medium) as medium,
            if(last_non_null_traffic_source.source = 'tagassistant.google.com', null, last_non_null_traffic_source.campaign) as campaign
        ) as last_non_null_traffic_source
    FROM session_first_last_source
), 

  get_last_non_direct_traffic_source as (
  select
    a.*,
    b.session_start_datetime,
    date(b.session_start_datetime) as session_start_date,
    b.has_session_start_event,
    b.session_engaged,
    b.engagement_time_seconds,
    b.user_first_visit_datetime,
    if(coalesce(nullif(a.first_traffic_source.source, '(direct)'), nullif(a.first_traffic_source.medium, '(none)'), nullif(a.first_traffic_source.campaign, '(direct)')) is null,
      last_value(if(coalesce(nullif(a.last_non_null_traffic_source.source, '(direct)'), nullif(a.last_non_null_traffic_source.medium, '(none)'), nullif(a.last_non_null_traffic_source.campaign, '(direct)')) is not null,
                    a.last_non_null_traffic_source,
                    null) ignore nulls)
      over(partition by a.user_key order by a.ga_session_id
      range between ${constants.ALL_EVENTS_LOOKBACK_WINDOW} preceding and 1 preceding
      ),
      a.first_traffic_source
     ) as last_non_direct_traffic_source
  from
    sessions_final a
    join ${ref('int_session')} b
    on a.user_key = b.user_key
    and a.ga_session_key = b.ga_session_key
), 

  add_channel_group as (
  select
    t.session_id,
    t.pages_in_session,
    t.landing_page,
    t.logged_in,
    t.user_key,
    t.ga_session_key,
    t.ga_session_id,
    t.user_pseudo_id,
    t.session_start_datetime,
    t.session_start_date,
    t.session_engaged,
    t.engagement_time_seconds,
    t.user_first_visit_datetime,
    (
      select
        as struct
        if(not t.has_session_start_event, '(not set)', ifnull(t.first_traffic_source.source, '(direct)')) as source,
        if(not t.has_session_start_event, '(not set)', ifnull(t.first_traffic_source.medium, '(none)')) as medium,
        if(not t.has_session_start_event, '(not set)', ifnull(t.first_traffic_source.campaign, '(direct)')) as campaign,
        if(not t.has_session_start_event, '(not set)', cf.source_category) as source_category,
        if(not t.has_session_start_event, 'Unassigned', ${helpers.groupChannels('t.first_traffic_source.source', 't.first_traffic_source.medium', 'cf.source_category')}) as channel_group
    ) as first_traffic_source,
    (
      select
        as struct
        if(not t.has_session_start_event, '(not set)', ifnull(t.last_non_direct_traffic_source.source, '(direct)')) as source,
        if(not t.has_session_start_event, '(not set)', ifnull(t.last_non_direct_traffic_source.medium, '(none)')) as medium,
        if(not t.has_session_start_event, '(not set)', ifnull(t.last_non_direct_traffic_source.campaign, '(direct)')) as campaign,
        if(not t.has_session_start_event, '(not set)', cl.source_category) as source_category,
        if(not t.has_session_start_event, 'Unassigned', ${helpers.groupChannels('t.last_non_direct_traffic_source.source', 't.last_non_direct_traffic_source.medium', 'cl.source_category')}) as channel_group
    ) as last_non_direct_traffic_source,
  from get_last_non_direct_traffic_source t
    left join ${ref('stg_source_categories')} cf
      on t.first_traffic_source.source = cf.source
    left join ${ref('stg_source_categories')} cl
      on t.last_non_direct_traffic_source.source = cl.source
)

select
  *
from add_channel_group