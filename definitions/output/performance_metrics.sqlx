/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
    type: "incremental",
    schema: constants.OUTPUT_DATASET,
    description: ".",
    tags: ["all"],
    bigquery: {
      partitionBy: "date"
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where date >= event_date_checkpoint`)
  }
}

  select
    d.date,
    platform,
    campaign_name,
    purpose,
    sum(reach) as reach,
    sum(impressions) as impressions,
    sum(cost) as cost,
    sum(clicks) as clicks
  from ${ref('dates')} as d
  left join 
    ( 
      select * from ${ref('meta_ads')} as m 
      union all
      select * from ${ref('google_ads')} as m
    ) c
    on d.date = c.date
    where d.date >= event_date_checkpoint
  group by
    1,
    2,
    3,
    4