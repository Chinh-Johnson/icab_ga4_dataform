/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    type: "table",
    schema: constants.INTERMEDIATE_DATASET,
    description: "Table that combines Google Ads click stats and campaign data from staging tables.",
    tags: ["all", "google_ads"]
}

with clickCampaign as (
  select
    cs.gclid as gclid,
    cs.campaign_id as campaign_id,
    ca.campaign_name as campaign_name,
    row_number() over (
      partition by gclid
      order by cs._data_date desc
    ) as row_num
  from ${ref('stg_google_ads_click_stats')} as cs
  left join ${ref('stg_google_ads_campaign')} as ca
    using (campaign_id)
)
select
  gclid,
  campaign_id,
  campaign_name
from clickCampaign
where row_num = 1