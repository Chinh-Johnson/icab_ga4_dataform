/**
* Copyright 2024 Google LLC
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*      http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "as IS" BasIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

config {
    type: "incremental",
    schema: constants.INTERMEDIATE_DATASET,
    description: "Intermediate model that updates staging Google Ads impressions.",
    tags: ["all", "google_ads"],
    bigquery: {
        partitionBy: "_data_date",
        clusterBy: ["campaign_id"]
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(_data_date) -3 from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  )
  ---
  ${when(incremental(),
    `delete from ${self()} where _data_date >= event_date_checkpoint`)
  }
}

    select
        _data_date,
        campaign_id,
        sum(metrics_impressions) as impressions,
    from
        ${ref('stg_google_ads_campaign_stats')} bcs
        where segments_click_type = 'URL_CLICKS' or segments_click_type = 'VIDEO_WEBSITE_CLICKS'
        and _data_date >= event_date_checkpoint
    group by
        1,
        2