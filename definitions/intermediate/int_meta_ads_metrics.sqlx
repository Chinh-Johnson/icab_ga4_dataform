/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

config {
    type: "incremental",
    schema: constants.INTERMEDIATE_DATASET,
    description: "Intermediate model for Google Ads campaign stats as cost etc.",
    tags: ["all"],
    bigquery: {
        partitionBy: "Date",
        clusterBy: ["Campaign_name"]
    }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(Date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where Date >= event_date_checkpoint`)
  }
}

  select
    Date as date,
    Publisher_platform as platform,
    Campaign_name as campaign_name,
    sum(Reach) as reach,
    sum(Impressions) as impressions,
    sum(Cost) as cost,
    sum(Clicks__all_) as clicks
  from ${ref('stg_meta')} as m
    where Date >= event_date_checkpoint
  group by
    1,
    2,
    3