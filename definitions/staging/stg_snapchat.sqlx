config {
  type: "incremental",
  schema: constants.STAGING_DATASET,
  description: "",
  tags: ["all"],
  bigquery: {
    partitionBy: "Date",
    clusterBy: ["Campaign_name"]
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(Date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where Date >= event_date_checkpoint`)
  }
}

with
  source as (
    select
      Date,
      Campaign_name,
      Impressions,
      Cost,
      Swipes,
      Unique_impressions
    from ${ref('snapchat_data')}
    where 
      Date >= event_date_checkpoint
  )
  
select
  *
from
  source
