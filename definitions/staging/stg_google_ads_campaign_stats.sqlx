/**
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
config {
  type: "incremental",
  schema: constants.STAGING_DATASET,
  description: "Staging model for Google Ads campaigns. Basic column casting and value extractions are performed here.",
  tags: ["google_ads"],
  bigquery: {
    partitionBy: "_data_date",
    clusterBy: ["campaign_id"]
  }
}

pre_operations {
  declare event_date_checkpoint default (
    ${when(incremental(),
      `select max(_data_date) - interval 3 day from ${self()}`,
      `select cast('${constants.START_DATE}' as date format 'yyyymmdd')`)
    }
  );
  ---
  ${when(incremental(),
    `delete from ${self()} where _data_date >= event_date_checkpoint`)
  }
}

with
  source as (
    select
      *
    from ${ref('ads_CampaignStats_' + constants.GADS_CUSTOMER_ID)}
    where _data_date >= event_date_checkpoint
  )
  
select
  *
from
  source
